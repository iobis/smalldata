plugins {
  id 'java'
  id 'application'
  id "io.franzbecker.gradle-lombok" version "2.1"
  id 'com.github.johnrengelman.shadow' version '4.0.4'
  id 'org.javamodularity.moduleplugin' version '1.4.0'
  id 'org.beryx.runtime' version '1.1.5'
  id 'checkstyle'
  id 'pmd'
}

repositories {
  mavenLocal()
  jcenter()
}

ext {
  vertxVersion = '3.6.3'
  jacksonVersion = '2.9.8'
  junitJupiterEngineVersion = '5.2.0'
  moduleName = 'obis.smalldata'
}

sourceCompatibility = 10

group = 'org.obis'
version = '1.0.0-SNAPSHOT'

mainClassName = "io.vertx.core.Launcher"

def mainVerticleName = "org.obis.smalldata.Starter"
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

runtime {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

dependencies {
  implementation "io.vertx:vertx-core:$vertxVersion"
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "io.vertx:vertx-web-client:$vertxVersion"
  implementation "io.vertx:vertx-mongo-client:$vertxVersion"
  implementation "io.vertx:vertx-web-api-contract:$vertxVersion"

  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion"
  implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"

  implementation "de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0"
  implementation "org.tinylog:tinylog:1.3.5"
  implementation "org.tinylog:slf4j-binding:1.3.5"

  compileOnly "org.projectlombok:lombok:1.18.6"

  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testImplementation "com.fasterxml.uuid:java-uuid-generator:3.1.5"
  testImplementation "commons-io:commons-io:2.6"

  testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion")
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

shadowJar {
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

checkstyle {
  ignoreFailures false
  maxErrors 0
  maxWarnings 0
  toolVersion "8.17"
}

pmd {
  consoleOutput true
  ruleSetConfig = resources.text.fromFile("config/pmd/ruleset.xml")
  ruleSets = []
}
